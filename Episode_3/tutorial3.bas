10 REM THIS PROGRAM IS PART 3 OF THE YM2151 TUTORIAL.
20 REM IT DEMONSTRATES HOW TO PLAY A SIMPLE TUNE WITH MULTIPLE
30 REM SIMULTANEOUS CHANNELS.

40 GOSUB 800  : REM INITIALIZE CHANNELS
50 GOSUB 1400 : REM INITIALIZE SEMITONE TABLE
60 GOSUB 1500 : REM PLAY MUSIC
70 END

800 REM INITIALIZE ALL THE CHANNELS
810 FOR C=0 TO 3
820 GOSUB 900
830 NEXT C
840 RETURN

900 REM INTIALIZE A SINGLE CHANNEL (NUMBER IN C)
910 READ V: POKE $9FE0, $20+C: POKE $9FE1, V: REM WAVEFORM
920 READ V: POKE $9FE0, $80+C: POKE $9FE1, V: REM ATTACK RATE
930 READ V: POKE $9FE0, $A0+C: POKE $9FE1, V: REM DECAY RATE
940 READ V: POKE $9FE0, $C0+C: POKE $9FE1, V: REM SUSTAIN RATE
950 READ V: POKE $9FE0, $E0+C: POKE $9FE1, V: REM RELEASE RATE + SUSTAIN LEVEL
960 RETURN

1000 REM INITIALIZE CHANNEL 0
1010 DATA $C7, $1F, $0A, $00, $FF
1100 REM INITIALIZE CHANNEL 1
1110 DATA $C7, $1F, $0A, $00, $FF
1200 REM INITIALIZE CHANNEL 2
1210 DATA $C7, $1F, $0A, $00, $FF
1300 REM INITIALIZE CHANNEL 3
1310 DATA $C7, $1F, $0A, $00, $FF

1400 REM INITIALIZE SEMITONE TABLE
1410 DATA 10, 13, 14, 1, 4, 5, 8
1420 DIM N(7)
1430 FOR I=0 TO 6
1440 READ N(I)
1450 NEXT I
1460 RETURN

1500 NT=TI+2
1510 IF TI<>NT THEN GOTO 1510
1520 FOR C=0 TO 3
1530 READ N$
1540 IF N$="X" THEN RETURN
1550 IF N$<>"" THEN GOSUB 1600
1560 NEXT C
1570 NT=NT+30
1580 GOTO 1510

1600 REM THIS PLAYS THE NOTE IN N$ ON CHANNEL C.
1610 K=ASC(N$)-ASC("A")                         : REM SEMITONE
1620 O=ASC(MID$(N$,2))-ASC("0")                 : REM OCTAVE
1630 IF K=2 THEN O=O-1                          : REM ADJUST FOR C
1640 POKE $9FE0, $08: POKE $9FE1, C             : REM KEY OFF
1650 POKE $9FE0, $28+C: POKE $9FE1, O*16+N(K)   : REM SET KEY CODE
1660 POKE $9FE0, $08: POKE $9FE1, C+8           : REM KEY ON
1670 RETURN

2000 REM THE MUSICAL SCORE STARTS HERE.
2010 REM FOUR VALUES IN EACH LINE, ONE FOR EACH CHANNEL.
2020 REM EACH LINE CORRESPONDS TO 1/8 OF A BAR.
2030 REM "" MEANS CONTINUE PREVIOUS NOTE.

2100 REM TWINKLE, TWINKLE LITTLE STAR. (C C F C)
2110 DATA "C5", "C4", "E4", "G4"
2120 DATA "C5", "",   "",   ""
2130 DATA "G5", "C4", "E4", "G4"
2140 DATA "G5", "",   "",   ""
2150 DATA "A5", "F4", "A4", "C5"
2160 DATA "A5", "",   "",   ""
2170 DATA "G5", "C4", "E4", "G4"
2180 DATA "",   "",   "",   ""

2200 REM HOW I WONDER WHAT YOU ARE. (F C G C)
2210 DATA "F5", "F4", "A4", "C5"
2220 DATA "F5", "",   "",   ""
2230 DATA "E5", "C4", "E4", "G4"
2240 DATA "E5", "",   "",   ""
2250 DATA "D5", "G4", "B4", "D5"
2260 DATA "D5", "",   "",   ""
2270 DATA "C5", "C4", "E4", "G4"
2280 DATA "",   "",   "",   ""

2300 REM UP ABOVE THE WORLD SO HIGH, (C F C G)
2310 DATA "G5", "C4", "E4", "G4"
2320 DATA "G5", "",   "",   ""
2330 DATA "F5", "F4", "A4", "C5"
2340 DATA "F5", "",   "",   ""
2350 DATA "E5", "C4", "E4", "G4"
2360 DATA "E5", "",   "",   ""
2370 DATA "D5", "G4", "B4", "D5"
2380 DATA "",   "",   "",   ""

2400 REM LIKE A DIAMOND IN THE SKY. (C F C G)
2410 DATA "G5", "C4", "E4", "G4"
2420 DATA "G5", "",   "",   ""
2430 DATA "F5", "F4", "A4", "C5"
2440 DATA "F5", "",   "",   ""
2450 DATA "E5", "C4", "E4", "G4"
2460 DATA "E5", "",   "",   ""
2470 DATA "D5", "G4", "B4", "D5"
2480 DATA "",   "",   "",   ""

2500 REM TWINKLE, TWINKLE LITTLE STAR. (C C F C)
2510 DATA "C5", "C4", "E4", "G4"
2520 DATA "C5", "",   "",   ""
2530 DATA "G5", "C4", "E4", "G4"
2540 DATA "G5", "",   "",   ""
2550 DATA "A5", "F4", "A4", "C5"
2560 DATA "A5", "",   "",   ""
2570 DATA "G5", "C4", "E4", "G4"
2580 DATA "",   "",   "",   ""

2600 REM HOW I WONDER WHAT YOU ARE. (F C G C)
2610 DATA "F5", "F4", "A4", "C5"
2620 DATA "F5", "",   "",   ""
2630 DATA "E5", "C4", "E4", "G4"
2640 DATA "E5", "",   "",   ""
2650 DATA "D5", "G4", "B4", "D5"
2660 DATA "D5", "",   "",   ""
2670 DATA "C5", "C4", "E4", "G4"
2680 DATA "",   "",   "",   ""

2700 DATA "X": REM INDICATE END OF MUSIC
